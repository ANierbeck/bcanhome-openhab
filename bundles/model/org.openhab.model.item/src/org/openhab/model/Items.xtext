grammar org.openhab.model.Items with org.eclipse.xtext.common.Terminals

generate items "http://www.openhab.org/model/Items"

ItemModel :
	(items+=ModelItem)*
;

ModelItem :
	(ModelNormalItem | ModelGroupItem) name=ID
	(label=STRING)?
	('<' icon=(ID|STRING) '>')?
	('(' groups+=[ModelGroupItem] (',' groups+=[ModelGroupItem])* ')')? 
	('{' bindings+=ModelBinding (',' bindings+=ModelBinding)* '}')* 
;

ModelGroupItem :
	'Group' {ModelGroupItem} ('<' type=ModelItemType '>' (function=ModelGroupFunction '['(activeState=(ID|STRING) ',' passiveState=(ID|STRING) ']')?)?)?
;

enum ModelGroupFunction :
	AND='AND' | OR='OR'
;

ModelNormalItem :
	type=ModelItemType
;

ModelItemType :
	'Switch' | 'Rollershutter' | 'Number' | 'String' | 'Dimmer' | 'Contact' | ID
;

ModelBinding:
	type=ID '=' configuration=STRING
;
