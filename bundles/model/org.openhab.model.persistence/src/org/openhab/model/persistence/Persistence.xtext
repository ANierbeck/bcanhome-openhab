grammar org.openhab.model.persistence.Persistence with org.eclipse.xtext.common.Terminals

import "http://www.openhab.org/model/Items"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate persistence "http://www.openhab.org/model/persistence/Persistence"

PersistenceModel:
	{PersistenceModel}
	'Strategies' '{' strategies+=Strategy* '}'
	('Filters' '{' filters+=Filter* '}')?
	('Items' '{' configs+=PersistenceConfiguration* '}')?
;

Strategy:
	(default?='default')? name=ID ':' definition=(CronStrategy | UpdateStrategy | ChangeStrategy)
;

CronStrategy:
	{CronStrategy}
	  cron=STRING
;

UpdateStrategy:
	{UpdateStrategy}
	'eachUpdate'
;

ChangeStrategy:
	{ChangeStrategy}
	'eachChange'
;

Filter:
	name=ID ':' definition=FilterDetails
;

FilterDetails:
	ThresholdFilter | TimeFilter
;

ThresholdFilter:
	'>' value=DECIMAL percent?='%'
;

TimeFilter:
	value=INT unit=('s' | 'm' | 'h' | 'd') 
;

PersistenceConfiguration:
	items+=(AllConfig | ItemConfig | GroupConfig) (',' items+=(AllConfig | ItemConfig | GroupConfig))* ('->' alias=STRING)? 
	((':' ('strategy' '=' strategies+=[Strategy|ID] (',' strategies+=[Strategy|ID])*)? 
		 ('filter' '=' filters+=[Filter|ID] (',' filters+=[Filter|ID])*)?) 
		| ';')
;

AllConfig:
	{AllConfig}
	'*'
;

ItemConfig:
	item=[ModelItem|ID]
;

GroupConfig:
	group=[ModelGroupItem|ID] '*'
;

DECIMAL returns ecore::EBigDecimal :
	INT ('.' INT)?
;
