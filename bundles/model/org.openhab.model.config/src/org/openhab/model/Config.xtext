grammar org.openhab.model.Config with org.eclipse.xtext.common.Terminals

generate config "http://www.openhab.org/model/Config"

Model :
	(items+=Item)*
;

Item :
	NormalItem | GroupItem
;

GroupItem :
	'Group' name=ID
	(label=STRING)?
	('<' icon=(ID|STRING) '>')?
	 ('(' groups+=[GroupItem] (',' groups+=[GroupItem])* ')')?
;

NormalItem :
	type=('Switch' | 'Rollerblind' | 'Measurement' | 'String' | 'Dimmer' | 'Contact' | ID) name=ID
	(label=STRING)?
	('<' icon=(ID|STRING) '>')?
	('(' groups+=[GroupItem] (',' groups+=[GroupItem])* ')')? 
	('{' bindings+=Binding (',' bindings+=Binding)* '}')* 
;

Binding:
	type=ID '=' configuration=STRING
;
